import Toybox.Application;
import Toybox.Complications;
import Toybox.Lang;
import Toybox.System;
import Toybox.WatchUi;

(:background_excluded)
class Segment34Delegate extends WatchUi.WatchFaceDelegate {
    var screenW = null;
    var screenH = null;
    var view as Segment34View;

    public function initialize(v as Segment34View) {
        WatchFaceDelegate.initialize();
        screenW = System.getDeviceSettings().screenWidth;
        screenH = System.getDeviceSettings().screenHeight;
        view = v;
    }

    public function onPress(clickEvent as WatchUi.ClickEvent) {
        var coords = clickEvent.getCoordinates();
        var x = coords[0];
        var y = coords[1];

        if(y < screenH / 3) {
            handlePress("pressToOpenTop");
        } else if (y < (screenH / 3) * 2) {
            handlePress("pressToOpenMiddle");
        } else if (x < screenW / 3) {
            handlePress("pressToOpenBottomLeft");
        } else if (x < (screenW / 3) * 2) {
            handlePress("pressToOpenBottomCenter");
        } else {
            handlePress("pressToOpenBottomRight");
        }

        return true;
    }

    function handlePress(areaSetting as String) {
        var cID = Application.Properties.getValue(areaSetting) as Complications.Type;

        if(cID == -1) {
            switch(view.nightModeOverride) {
                case 1:
                    view.nightModeOverride = 0;
                    view.resolver.infoMessage = "DAY THEME";
                    break;
                case 0:
                    view.nightModeOverride = -1;
                    view.resolver.infoMessage = "THEME AUTO";
                    break;
                default:
                    view.nightModeOverride = 1;
                    view.resolver.infoMessage = "NIGHT THEME";
            }
            view.onSettingsChanged();
        }

        if(cID == -2 || cID == -3) { // Increase / decrease counter
            var count = Application.Storage.getValue("dailyCounter") as Number?;
            var newCount = (count != null ? count : 0) + (cID == -2 ? 1 : -1);
            Application.Storage.setValue("dailyCounter", newCount);
            view.forceDataRefresh();
            WatchUi.requestUpdate();
        }

        if(cID == -4) { // Open the on-watch complication picker
            try {
                WatchUi.pushView(
                    new ComplicationRootMenu(),
                    new ComplicationRootDelegate(view),
                    WatchUi.SLIDE_LEFT
                );
            } catch (e) {}
        }

        if(cID != null and cID > 0) {
            try {
                Complications.exitTo(new Id(cID));
            } catch (e) {}
        }
    }

}
